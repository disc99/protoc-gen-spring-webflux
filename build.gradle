buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.1.0"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.1.8.RELEASE"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

project('plugin') {

    apply plugin: 'java'
    apply plugin: 'application'
    apply plugin: 'idea'
    apply plugin: "com.github.johnrengelman.shadow"

    sourceCompatibility = JavaVersion.VERSION_1_8

    dependencies {
        implementation 'com.google.protobuf:protobuf-java:3.6.1'
        implementation 'com.google.protobuf:protobuf-java-util:3.6.1'
        implementation 'io.grpc:grpc-protobuf:1.19.0'
        implementation 'com.github.jknack:handlebars:4.1.2'
        implementation 'org.apache.commons:commons-lang3:3.9'
        implementation 'com.google.googlejavaformat:google-java-format:1.7'
        implementation 'org.apache.logging.log4j:log4j-api:2.12.1'

        testRuntime 'org.junit.platform:junit-platform-launcher:1.4.2'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
        testRuntime 'org.junit.vintage:junit-vintage-engine:5.4.2'
    }

    // Define the main class for the application
    mainClassName = 'io.disc99.protoc.gen.spring.Main'

    jar {
        manifest {
            attributes "Main-Class" : "io.disc99.protoc.gen.spring.Main"
        }
    }
    shadowJar {
        baseName = 'protoc-gen-spring-webflux'
        classifier = null
    }
}

project('example') {

    apply plugin: 'java'
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'com.google.protobuf'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-json'
        implementation 'io.github.lognet:grpc-spring-boot-starter:3.1.0'
        implementation "io.grpc:grpc-netty:1.23.0"
        implementation "io.grpc:grpc-protobuf:1.23.0"
        implementation "io.grpc:grpc-stub:1.23.0"

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude module: 'junit'
        }
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    }


    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:3.8.0"
        }
        plugins {
            grpc {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.21.0'
            }
            webflux {
                path = '/usr/local/bin/protoc-gen-spring-webflux'
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {}
                webflux {
                    option 'style=rest'
                }
            }
        }
    }

    // for IDEA
    sourceSets {
        main {
            java {
                srcDirs 'build/generated/source/proto/main/grpc'
                srcDirs 'build/generated/source/proto/main/java'
                srcDirs 'build/generated/source/proto/main/webflux'
            }
        }
    }
}

